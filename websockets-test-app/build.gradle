buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.8.2"
        classpath "org.grails.plugins:hibernate4:5.0.6"
    }
}

version "0.1"
group "websockets.test.app"

apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"org.grails.grails-gsp"
apply plugin:"asset-pipeline"
apply from: "$rootDir/gradle/saucelabs.gradle"

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

grails {
    plugins {
        compile project(':grails-plugin')
    }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-dependencies"
    compile "org.grails:grails-web-boot"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:scaffolding"
    compile "org.grails.plugins:hibernate4"
    compile "org.hibernate:hibernate-ehcache"
    compile "org.grails.plugins:grails-spring-websocket:2.3.0"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web:${grailsVersion}"
    runtime "com.bertramlabs.plugins:asset-pipeline-grails:2.8.2"
    runtime "com.h2database:h2"
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails.plugins:geb"
    testCompile "org.seleniumhq.selenium:selenium-support:2.53.0"
    if (System.getProperty("geb.saucelabs.browser")) {
        testCompile "org.seleniumhq.selenium:selenium-remote-driver:2.53.0"
    } else {
        testCompile "org.seleniumhq.selenium:selenium-firefox-driver:2.53.0"
    }
}

assets {
    minifyJs = true
    minifyCss = true
}

sauceLabs {
    browsers {
        firefox_linux_19
        //chrome_mac
        delegate."internet explorer_vista_10"
    }
    task {
        testClassesDir = integrationTest.testClassesDir
        testSrcDirs = integrationTest.testSrcDirs
        classpath = integrationTest.classpath
    }
    account {
        username = System.getenv('GEB_SAUCE_LABS_USER') ?: getProperty('sauceLabs.username')
        accessKey = System.getenv('GEB_SAUCE_LABS_ACCESS_PASSWORD') ?: getProperty('sauceLabs.accessKey')
    }
}
