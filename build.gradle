buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath("org.grooscript:grooscript:1.2.0") {
            exclude module: 'groovy'
            exclude module: 'groovy-json'
        }
    }
}

plugins {
    id "io.spring.dependency-management" version "0.5.2.RELEASE"
    id "com.jfrog.bintray" version "1.2"
    id "org.asciidoctor.convert" version "1.5.2"
}

version "1.1.1"
group "org.grails.plugins"

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "spring-boot"
apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-gsp"

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
    grooscriptVersion = '1.2.0'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    provided 'org.springframework.boot:spring-boot-starter-logging'
    provided "org.springframework.boot:spring-boot-starter-actuator"
    provided "org.springframework.boot:spring-boot-autoconfigure"
    provided "org.springframework.boot:spring-boot-starter-tomcat"

    provided "org.grails:grails-web-boot"
    provided "org.grails:grails-dependencies"
    provided 'javax.servlet:javax.servlet-api:3.1.0'

    testCompile "org.grails:grails-plugin-testing"
    compile "org.grails.plugins:asset-pipeline"
    compile "org.grails.plugins:cache"
    compile("org.grooscript:grooscript:${grooscriptVersion}") {
        exclude module: 'groovy'
        exclude module: 'groovy-json'
    }

    console "org.grails:grails-console"
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

jar {
    exclude "test/**/**"
}

bintray {
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_key') ? bintray_key : ''

    publications = ['maven']
    publish = true
    pkg {
        repo = 'grooscript'
        userOrg = ''
        name = "${project.group}:grooscript"
        desc = 'Grooscript Grails 3 Plugin'
        websiteUrl = 'http://grooscript.org/grails3-plugin'
        issueTrackerUrl = "https://github.com/chiquitinxx/grooscript-grails3-plugin/issues"
        vcsUrl = "https://github.com/chiquitinxx/grooscript-grails3-plugin"
        licenses = ['Apache-2.0']
        labels = ['grails', 'javascript', 'grooscript', 'groovy', 'plugin']
        version {
            attributes = ['grails-plugin': "org.grails.plugins:grooscript"]
            name = project.version
            desc = 'Upgrade to grooscript 1.2.0'
            gpg {
                sign = false
                passphrase = System.getenv("SIGNING_PASSPHRASE") ?: project.hasProperty("signingPassphrase") ? project.signingPassphrase : ''
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom.withXml {
                def pomNode = asNode()
                pomNode.dependencyManagement.replaceNode {}

                // simply remove dependencies without a version
                // version-less dependencies are handled with dependencyManagement
                // see https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/8 for more complete solutions
                pomNode.dependencies.dependency.findAll {
                    it.version.text().isEmpty()
                }.each {
                    it.replaceNode {}
                }
            }
            artifactId project.name
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

asciidoctor {
    sourceDir = project.file 'src/main/doc'
    sources { include 'index.adoc' }
    attributes 'grooscript-version': grooscriptVersion,
            'plugin-version': version,
            author: 'Jorge Franco',
            email: 'grooscript@gmail.com',
            'source-highlighter': 'prettify',
            toc: 'right',
            imagesdir: './images',
            toclevels: 2,
            icons: 'font'
}

import org.grooscript.GrooScript
import org.grooscript.convert.ConversionOptions

task generateJavascriptFile() << {
    String text = GrooScript.classLoader.getResourceAsStream('META-INF/resources/grooscript.js').text
    text += GrooScript.classLoader.getResourceAsStream('META-INF/resources/grooscript-tools.js').text

    def conversionOptions = [classpath: ['src/main/groovy']]
    text += GrooScript.convert(new File('src/main/groovy/org/grooscript/grails/util/GrooscriptGrails.groovy').text, conversionOptions)
    text += GrooScript.convert(new File('src/main/groovy/org/grooscript/grails/promise/RemoteDomain.groovy').text, conversionOptions)
    conversionOptions[ConversionOptions.FINAL_TEXT.text] = 'var gsEvents = ClientEventHandler();'
    text += GrooScript.convert(new File('src/main/groovy/org/grooscript/grails/event/ClientEventHandler.groovy').text, conversionOptions)

    new File('grails-app/assets/javascripts/grooscript-grails.js').text = text
}

classes.dependsOn 'generateJavascriptFile'

