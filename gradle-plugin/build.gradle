buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
    }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.3"
    id "com.github.hierynomus.license" version "0.13.1"
}

apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply from: "$projectDir/gradle/publishing.gradle"
apply from: "$projectDir/gradle/functional-test.gradle"
apply plugin: "com.gradle.plugin-publish"

group = 'org.grooscript'
version = '1.3.0'

sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: "groovy-all"
    }
    testCompile 'cglib:cglib-nodep:3.1'
    compile("org.grooscript:grooscript:${grooscriptVersion}") {
        exclude module: "groovy"
        exclude module: "groovy-json"
    }
    compile('org.codehaus.gpars:gpars:1.2.1') {
        exclude module: 'groovy-all'
    }
    compile 'org.grooscript:grails-core:0.0.1'
    compile "org.springframework:spring-websocket:${springVersion}"
    compile "org.springframework:spring-messaging:${springVersion}"
    compile 'org.apache.tomcat.embed:tomcat-embed-websocket:8.0.15'
    compile 'org.apache.tomcat.embed:tomcat-embed-logging-juli:8.0.15'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.4'
    testCompile 'org.objenesis:objenesis:2.5.1'
}

asciidoctor {
    sourceDir = project.file 'doc'
    sources { include 'tasks.adoc', 'templating.adoc' }
    attributes 'plugin-version': version,
            'gradle-version': project.properties['gradleVersion'],
            'grooscript-version': project.properties['grooscriptVersion'],
            author: 'Jorge Franco',
            email: 'grooscript@gmail.com',
            'source-highlighter': 'prettify',
            icons: 'font',
            'index-file': 'index.html'
}

license {
    header = file('LICENSE.txt')
    include "**/*.groovy"
    includes(["**/*.java"])
}

pluginBundle {
    website = 'http://grooscript.org/'
    vcsUrl = 'https://github.com/chiquitinxx/grooscript-plugins/tree/master/gradle-plugin'
    description = 'Convert groovy code to javascript with grooscript'
    tags = ['groovy', 'js']

    plugins {
        grooscriptGradlePlugin {
            id = 'org.grooscript.conversion'
            displayName = 'Grooscript gradle plugin'
        }
    }
}